apply plugin: 'java-library'
apply plugin: 'idea'
apply plugin: 'jacoco'
apply plugin: 'maven-publish'

apply from: "${rootDir}/gradle/publish.gradle"

repositories {
    mavenCentral()

    maven {
        url 'https://hub.spigotmc.org/nexus/content/repositories/public/'
    }

    maven {
        url 'https://repo.md-5.net/content/groups/public/'
    }
}

dependencies {
    api "org.spigotmc:spigot-api:1.16.5-R0.1-SNAPSHOT"
    implementation 'org.junit.jupiter:junit-jupiter:5.7.1'
    implementation 'org.hamcrest:hamcrest-library:1.3'
    implementation 'org.apache.commons:commons-io:1.3.2'
    implementation 'org.jetbrains:annotations:19.0.0'
}

task javadocJar(type: Jar) {
    classifier('javadoc')
    from javadoc
}

task sourcesJar(type: Jar) {
    classifier('sources')
    from sourceSets.main.allSource
}

tasks.withType(JavaCompile) {
    Provider<JavaCompiler> compilerProvider = javaToolchains.compilerFor {
        JavaLanguageVersion.of(8)
    }
    JavaCompiler compiler = compilerProvider.orNull

    if (compiler != null && compiler.metadata.languageVersion.asInt() > 9) {
        // Mark release as 8 if the compiler is Java 10 or newer
        options.release.set(8)
    } else {
        // Only use the legacy compiler options otherwise
        sourceCompatibility = 1.8
        targetCompatibility = 1.8
    }
}

artifacts {
    archives javadocJar, sourcesJar
}

def isFork = true

jacocoTestReport {
    reports {
        xml.enabled true
    }
}

jacoco {
	toolVersion = "0.8.6"
}

test {
    useJUnitPlatform()
}
